plugins {
    id 'java'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '1.8.0'
    id "org.sonarqube" version "2.8"
    id "checkstyle"
}

version = "${project_version}" + getBuildNumber() + isSnapshot()
group = "${project_group}"
archivesBaseName = "${project_name}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven { url "https://oss.jfrog.org/artifactory/libs-release" }
}

dependencies {
    compile group: 'io.undertow', name: 'undertow-core', version: '2.0.27.Final'
    compile group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.7.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.12.1'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'

    compile group: 'com.zaxxer', name: 'HikariCP', version: '3.4.1'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '6.0.8'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.8'

    compile group: 'com.github.twitch4j', name: 'twitch4j', version: '1.0.0-alpha.17'

    testImplementation('org.junit.jupiter:junit-jupiter:5.5.2')
}

jar {
    manifest {
        attributes 'Implementation-Title': 'PlumeAlertsAPI',
                'Main-Class': 'com.plumealerts.api.PlumeAlertsAPI'
    }
}

jib {
    to {
        image = "plumealerts/plumealerts-api:" + version
        tags = ['latest' + isSnapshot()]
        auth {
            username = System.getenv("DOCKER_USERNAME")
            password = System.getenv("DOCKER_PASSWORD")
        }
    }
}

checkstyle {
    configFile = file("checkstyle.xml")
    toolVersion = '8.10'
}

sonarqube {
    properties {
        property "sonar.projectName", "PlumeAlerts-API"
        property "sonar.projectKey", "PlumeAlerts-API"
    }
}

static String getBuildNumber() {
    return System.getenv("TRAVIS_BUILD_NUMBER") ?: System.getenv("BUILD_NUMBER") ?: "0"
}

static String isSnapshot() {
    return System.getenv("TRAVIS_TAG") ? "" : "-snapshot"
}